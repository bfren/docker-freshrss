#!/usr/bin/nu

use bf
use bf-php

# Absolute path to WordPress source directory
const src = "/etc/bf/src/freshrss"

def main [] {
    # installed required and recommended extensions
    cd /tmp
    bf write $"Installing PHP extensions."
    bf-php ext install_from_file

    # install FreshRSS source files, content directory and default plugins
    bf write debug $"FreshRSS source directory: ($src)."
    install_source

    # add bf-freshrss module to config
    bf config use bf-freshrss

    # cleanup
    bf write "Removing /www files and directories."
    rm -rf /www/*
}

# Download and install FreshRSS source files
def install_source [] {
    # add dependencies
    bf write "Adding installation dependencies." install_source
    bf pkg install ["--virtual .install" "git"]

    # get version
    let fr_version = bf fs read FRESHRSS_REVISION

    # save FreshRSS version to build log
    bf build add FreshRSS $fr_version

    # clone source files
    bf write $"Cloning FreshRSS ($fr_version) into ($src)."
    ^git clone --depth 1 --branch $fr_version https://github.com/FreshRSS/FreshRSS $src

    # set permissions
    bf write $"Setting permissions on ($src)." install_source
    [$src "www:www" 0400 0500] | bf ch apply
    cd $src
    ^sh cli/access-permissions.sh

    # remove installation dependencies
    bf pkg remove [".install"]
}
